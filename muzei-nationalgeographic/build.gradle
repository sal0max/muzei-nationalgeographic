/*
 * Copyright 2014 Maximilian Salomon.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see http://www.gnu.org/licenses/.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
   // kotlin
   implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
   implementation 'androidx.core:core-ktx:1.3.2'
   // muzei
   implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'
   // retrofit
   implementation 'com.squareup.retrofit2:retrofit:2.9.0'
   implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
   implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
   // misc
   implementation 'android.arch.work:work-runtime-ktx:1.0.1'
   implementation 'androidx.preference:preference-ktx:1.1.1'
   implementation 'com.github.sisyphsu:dateparser:1.0.7'
   // test
   testImplementation 'junit:junit:4.13.2'
}

android {
   compileSdkVersion 30
   compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
   }
   kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8.toString()
   }

   defaultConfig {
      applicationId 'de.msal.muzei.nationalgeographic'
      minSdkVersion 21
      targetSdkVersion 30
      // SemVer
      versionName = "2.3.0"
      versionCode = 20300
      archivesBaseName = "$applicationId-v$versionCode"
      //
      vectorDrawables.useSupportLibrary = true
   }

   signingConfigs {
      release {
         /* get the keystore data for signing */
         Properties props = new Properties()
         def propFile = file('../keystore/signing.properties')

         if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))

            if (props != null
                  && props.containsKey('STORE_FILE')
                  && props.containsKey('STORE_PASSWORD')
                  && props.containsKey('KEY_ALIAS')
                  && props.containsKey('KEY_PASSWORD')) {
               android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
               android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
               android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
               android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
            } else {
               android.buildTypes.release.signingConfig = null
               println 'props == null'
            }
         } else {
            android.buildTypes.release.signingConfig = null
            println 'propFile can\'t be read.'
         }
      }
   }

   buildTypes {
      release {
         signingConfig signingConfigs.release
         debuggable false
         minifyEnabled true
         zipAlignEnabled true
         jniDebuggable false
         proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
      debug {
         applicationIdSuffix '.debug'
         versionNameSuffix ' [DEBUG]'
      }
   }

   lintOptions {
      disable 'InvalidPackage'
   }

   testOptions {
      unitTests.returnDefaultValues = true
   }

}

// versionCode <-> versionName /////////////////////////////////////////////////////////////////////

/**
 * Checks if versionCode and versionName match.
 * Needed because of F-Droid: both have to be hard-coded and can't be assigned dynamically.
 * So at least check during build for them to match.
 */
task checkVersion doLast {
    int versionCode = android.defaultConfig.versionCode
    int correctVersionCode = generateVersionCode(android.defaultConfig.versionName)
    if (versionCode != correctVersionCode)
        throw new GradleException(
                "versionCode and versionName don't match: " +
                        "versionCode should be $correctVersionCode. Is $versionCode."
        )
}
assemble.dependsOn checkVersion

/**
 * Checks if a fastlane changelog for the current version is present.
 */
task checkFastlaneChangelog doLast {
    int versionCode = android.defaultConfig.versionCode
    File changelogFile = file("$rootDir/fastlane/metadata/android/en-US/changelogs/${versionCode}.txt")
    if (!changelogFile.exists())
        throw new GradleException(
                "Fastlane changelog missing: expecting file '$changelogFile'"
        )
}
build.dependsOn checkFastlaneChangelog

/**
 * Generates a versionCode based on the given semVer String.
 * See e.g. https://proandroiddev.com/keep-salinity-with-your-versioncode-db2089b640b9
 *
 * @param semVer e.g. 1.3.1
 * @return e.g. 10301 (-> 1 03 01)
 */
private static int generateVersionCode(String semVer) {
    return semVer.split('\\.')
            .collect { Integer.parseInt(it) }
            .inject { sum, value -> sum * 100 + value }
}